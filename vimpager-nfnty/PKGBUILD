pkgname=('vimpager-nfnty')
srcname='vimpager'
pkgver=r5.6859875
pkgrel=1
pkgdesc='Vim as pager with escape sequences stripped'
arch=('any')
url='https://github.com/nfnty/vimpager'
license=('MIT')

depends=('vim')
makedepends=('git')
provides=('vimpager')
conflicts=('vimpager')

source=("$srcname::git+https://github.com/nfnty/vimpager.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$srcname"
	printf 'r%s.%s\n' \
		"$(git rev-list HEAD | wc -l)" \
		"$(git describe --always | sed 's/-/./g')"
}

package() {
	cd "$srcdir/$srcname"
	install -D -m755 vimpager.sh "$pkgdir/usr/bin/vimpager"
	install -D -m755 manpager.sh "$pkgdir/usr/bin/manpager"
	install -D -m755 diffpager.sh "$pkgdir/usr/bin/diffpager"
	install -D -m755 gitpager.sh "$pkgdir/usr/bin/gitpager"
}
