pkgname=('vimperator-git')
_srcname='vimperator'
pkgver=r4842.69e52fd
pkgrel=1
pkgdesc='Make Firefox look and behave like Vim'
arch=('any')
url='https://github.com/vimperator/vimperator-labs'
license=('MIT')

depends=('firefox')
makedepends=('git')
provides=('vimperator')
conflicts=('vimperator' 'vimperator-hg')

source=("$_srcname::git+https://github.com/vimperator/vimperator-labs.git" 'no_zip.patch')
sha256sums=('SKIP' 'faec2c61cbfbe39084ad3275d3b70f10ce0be9f5498d8be0a15e6336c5ad1fa5')

_extension_id='vimperator@mozdev.org'

pkgver() {
	cd "$_srcname"
	printf 'r%s.%s\n' "$(git rev-list HEAD | wc -l)" "$(git describe --always)"
}

prepare() {
	cd "$_srcname"
	git apply ../no_zip.patch
}

build() {
	cd "${_srcname}/vimperator"
	make VERSION="$pkgver" xpi
}

package() {
	local destdir="${pkgdir}/usr/lib/firefox/browser/extensions"
	install -d "$destdir"
	cp -R "${_srcname}/downloads/vimperator-$pkgver" "${destdir}/$_extension_id"
}
