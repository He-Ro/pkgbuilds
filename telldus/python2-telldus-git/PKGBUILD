pkgname=('python2-telldus-git')
srcname='telldus'
pkgver='r1'
pkgrel='1'
pkgdesc='Python 2 bindings for Telldus Technologies TellStick'
arch=('any')
url='https://github.com/telldus/telldus'
license=('LGPL2')

depends=('python2' 'telldus-core')
makedepends=('git' 'python2-setuptools')
provides=('python2-telldus')
conflicts=('python2-telldus')

source=(
    "${srcname}::git+https://github.com/telldus/telldus.git"
)
sha512sums=(
    'SKIP'
)

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

package() {
    cd "${srcdir}/${srcname}/bindings/python/native"

    python2 setup.py install --root="${pkgdir}" --optimize=1
}
