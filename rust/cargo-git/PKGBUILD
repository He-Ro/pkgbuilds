pkgname=('cargo-git')
srcname='cargo'
pkgver='1'
pkgrel='1'
pkgdesc='The Rust package manager'
arch=('i686' 'x86_64')
url='https://github.com/rust-lang/cargo'
license=('MIT' 'Apache')

depends=('rust-git')
makedepends=('git' 'python' 'curl' 'cmake' 'pkg-config' 'openssl')
provides=('cargo')
conflicts=('cargo')

source=("${srcname}::git+https://github.com/rust-lang/cargo.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

build() {
    cd "${srcdir}/${srcname}"

    ./configure --prefix='/usr'
    make
}

package() {
    cd "${srcdir}/${srcname}"

    make DESTDIR="${pkgdir}" install
}
