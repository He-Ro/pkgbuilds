pkgname=('opensmtpd-git')
srcname='opensmtpd'
pkgver='r1'
pkgrel='1'
pkgdesc='Free implementation of the server-side SMTP protocol, with some additional standard extensions.'
arch=('i686' 'x86_64')
url='https://github.com/OpenSMTPD/OpenSMTPD'
license=('ISC' 'custom:BSD')

depends=('libasr' 'libevent' 'openssl' 'db')
makedepends=('git' 'bison' 'autoconf' 'automake' 'libtool')
provides=('opensmtpd')
conflicts=('opensmtpd')

source=(
    "${srcname}::git+https://github.com/OpenSMTPD/OpenSMTPD.git#branch=portable"
    'smtpd.service'
    'smtpd.socket'
    'lmtp.patch'
)
sha512sums=(
    'SKIP'
    '6068bda683dd7378811b818046a660501ddd04bcdbba27126f915393dd5740b674d6e6906b96ede9e661bd02a32474b5143838e9368d7554bdd992fdff050c38'
    '806025d000ba34e86338bf127951e853849e077fe6941f7d0f43c362303ed191554f1d983d422f1acec22e89091517237a60da6e7ae6db1cb977fffe666e48c4'
    'c298931dd90e0c074dfd6f83809691038b65933f0f1de0496d465777777ef47d3f9cbd5815f8d23763c6dc46136fc9f52119bdc64e587bf52d864f7ea2c70678'
)

install='opensmtpd.install'
backup=('etc/smtpd/smtpd.conf' 'etc/smtpd/aliases')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/lmtp.patch"

    sed -ri 's,/etc/mail,/etc/smtpd,g' smtpd/smtpd.conf

    ./bootstrap

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc/smtpd \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/smtpd \
        --with-maildir=/var/spool/mail \
        --with-privsep-path=/var/empty \
        --with-sock-dir=/run \
        --with-ca-file=/etc/ssl/certs/ca-certificates.crt \
        --with-privsep-user=smtpd \
        --with-queue-user=smtpq \
        --with-pam
}

build() {
    cd "${srcdir}/${srcname}"

    make
}

package() {
    cd "${srcdir}/${srcname}"

    make DESTDIR="${pkgdir}" install

    # install license and systemd unit files
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/${srcname}/LICENSE"
    install -D --mode=644 "${srcdir}/smtpd.service" "${pkgdir}/usr/lib/systemd/system/smtpd.service"
    install -D --mode=644 "${srcdir}/smtpd.socket" "${pkgdir}/usr/lib/systemd/system/smtpd.socket"

    # install an empty aliases file (used by the default config)
    install -D --mode=644 /dev/null "${pkgdir}/etc/smtpd/aliases"
}
