pkgname=('influxdb')
srcname='influxdb'
pkgdesc='Scalable datastore for metrics, events, and real-time analytics'
pkgver='0.10.1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/influxdb/${srcname}"
license=('MIT')

makedepends=('git' 'go' 'python2')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

_url_go="${url#*//}"
_url_git="${url}.git"

source=("${srcname}::git+${_url_git}")
sha512sums=('SKIP')

prepare() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    rm --recursive "${GOPATH}/src/${_url_go}"
    mv "${srcname}" "${GOPATH}/src/${_url_go}"

    cd "${GOPATH}/src/${_url_go}"
    git remote set-url origin "${_url_git}"
    git checkout --quiet "$( git rev-parse --abbrev-ref origin/HEAD | sed 's|^.*/||' )"
    go get -v -x -u -d ./...
    git checkout --quiet "v${pkgver}"
}

build() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    go install -v -x -ldflags="\
-X main.version=${pkgver} \
-X main.branch=$( git rev-parse --abbrev-ref HEAD ) \
-X main.commit=$( git rev-parse HEAD ) \
-X main.buildtime=$( date --utc --iso-8601=seconds )" \
        ./...
}

package() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    install --directory "${pkgdir}/"{"etc/${srcname}",usr,"var/lib/${srcname}"}
    cp --recursive "${GOPATH}/bin" "${pkgdir}/usr/"

    cd "${GOPATH}/src/${_url_go}"
    cp etc/config.sample.toml "${pkgdir}/etc/${srcname}/config.toml"

    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}"
}
