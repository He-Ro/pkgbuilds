pkgname=('lilyterm-git')
srcname='lilyterm'
pkgdesc='A lightweight, but functional terminal emulator'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/Tetralet/${srcname}"
license=('GPL3')

depends=('vte3')
makedepends=('git' 'gettext' 'intltool' 'pkgconfig')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=(
    "${srcname}::git+${url}.git"
    'vte_background.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
)

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/vte_background.patch"
}

build() {
    cd "${srcdir}/${srcname}"

    ./autogen.sh
    ./configure --prefix='/usr' --sysconfdir='/etc'

    make ENABLE_VTE_BACKGROUND='Y'
}

package() {
    cd "${srcdir}/${srcname}"

    make DESTDIR="${pkgdir}" install
}
