pkgname=('htop-git')
srcname='htop'
pkgver='1'
pkgrel='1'
pkgdesc='Interactive text-mode process viewer'
arch=('i686' 'x86_64')
url='https://github.com/hishamhm/htop'
license=('GPL2')

depends=('ncurses')
makedepends=('git' 'python2')
optdepends=(
    'lsof: List open files for running process'
    'strace: Attach to running process'
)
provides=('htop')
conflicts=('htop')

source=(
    "${srcname}::git+https://github.com/hishamhm/htop.git"
    'vim.patch'
)
sha512sums=(
    'SKIP'
    '99d32515d18d60a55e935c04c5765695dd7b4d4e0d2153e6bc780692e69db79a16033daac9506a019ac69f5ee103c4c715e2ee6f2b2cdc95a9cff00d3148bfaa'
)

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/vim.patch"

    ./autogen.sh

    ./configure \
        --prefix=/usr \
        --enable-unicode \
        --enable-openvz \
        --enable-vserver \
        --enable-cgroup \
        --enable-oom
}

build() {
    cd "${srcdir}/${srcname}"

    make
}

package() {
    cd "${srcdir}/${srcname}"

    make DESTDIR="${pkgdir}" install
}
