pkgname=('htop-git')
srcname='htop'
pkgver='r1'
pkgrel='1'
pkgdesc='Interactive text-mode process viewer'
arch=('i686' 'x86_64')
url='https://github.com/hishamhm/htop'
license=('GPL2')

depends=('ncurses')
makedepends=('git' 'python')
optdepends=(
    'lsof: List open files for running process'
    'strace: Attach to running process'
)
provides=('htop')
conflicts=('htop')

source=(
    "${srcname}::git+https://github.com/hishamhm/htop.git"
    'vim.patch'
)
sha512sums=(
    'SKIP'
    'e459cb18ae8741f4397de2b9ce7526a2aa6fb1a390b7852ce08c7d1cca14926ae9fc067c1d8c9da3eadf561402c68f41cb75b889a68088375fbfe9fb34b9a7f4'
)

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/vim.patch"

    ./autogen.sh

    ./configure \
        --prefix=/usr \
        --enable-unicode \
        --enable-openvz \
        --enable-vserver \
        --enable-cgroup \
        --enable-oom
}

build() {
    cd "${srcdir}/${srcname}"

    make
}

package() {
    cd "${srcdir}/${srcname}"

    make DESTDIR="${pkgdir}" install
}
