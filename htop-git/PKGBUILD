pkgname=('htop-git')
srcname='htop'
pkgver='r1'
pkgrel='1'
pkgdesc='Interactive text-mode process viewer'
arch=('i686' 'x86_64')
url='https://github.com/hishamhm/htop'
license=('GPL2')

depends=('ncurses')
makedepends=('git' 'python')
optdepends=(
    'lsof: List open files for running process'
    'strace: Attach to running process'
)
provides=('htop')
conflicts=('htop')

source=(
    "${srcname}::git+https://github.com/hishamhm/htop.git"
    'vim.patch'
    'starttime.patch'
)
sha512sums=(
    'SKIP'
    'SKIP'
    'SKIP'
)

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/vim.patch"
    git apply "${srcdir}/starttime.patch"

    ./autogen.sh

    ./configure \
        --prefix=/usr \
        --enable-unicode
}

build() {
    cd "${srcdir}/${srcname}"

    make
}

package() {
    cd "${srcdir}/${srcname}"

    make DESTDIR="${pkgdir}" install
}
