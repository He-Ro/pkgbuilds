pkgname=htop-git
srcname=htop
pkgver=r420.aed9b9d
pkgrel=1
pkgdesc="Interactive text-mode process viewer"
url="https://github.com/hishamhm/htop"
license=('GPL')
arch=('i686' 'x86_64')
depends=('ncurses')
makedepends=('git' 'python2')
optdepends=('lsof: list open files for running process'
            'strace: attach to running process')
provides=('htop')
conflicts=('htop')
source=(
    "git+https://github.com/hishamhm/htop.git"
    'vim.patch'
)
sha256sums=(
    'SKIP'
    'SKIP'
)

options=('!strip')

pkgver() {
    cd "$srcdir/$srcname"
    printf 'r%s.%s\n' \
        "$(git rev-list HEAD | wc -l)" \
        "$(git describe --always | sed 's/-/./g')"
}

prepare() {
     cd "${srcdir}/${srcname}"

     git apply "$srcdir/vim.patch"

     ./autogen.sh

     sed -i 's|ncursesw/curses.h|curses.h|' RichString.[ch] configure
     sed -i 's|python|python2|' scripts/MakeHeader.py

     ./configure \
         --prefix=/usr \
         --enable-unicode \
         --enable-openvz \
         --enable-vserver \
         --enable-cgroup \
         --enable-oom
}

build() {
     cd "${srcdir}/${srcname}"
     make debug
}

package() {
     cd "${srcdir}/${srcname}"
     make DESTDIR="${pkgdir}" install
}
