pkgname=('qterminal-git')
srcname='qterminal'
pkgdesc='A Qt-based terminal emulator'
pkgver='r1'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/lxde/${srcname}"
license=('GPL2')

depends=('qt5-base' 'qt5-x11extras' 'qtermwidget')
makedepends=('git' 'cmake' 'qt5-tools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${srcname}::git+${url}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s.%s\n' \
        "$( git rev-list --count 'HEAD' )" \
        "$( git log --max-count='1' --pretty='format:%ct' )" \
        "$( git rev-parse --short 'HEAD' )"
}

build() {
    mkdir --parents "${srcdir}/build"
    cd "${srcdir}/build"

    cmake \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_LIBDIR='lib' \
        -DCMAKE_BUILD_TYPE='Release' \
        "${srcdir}/${srcname}"

    make
}

package() {
    cd "${srcdir}/build"

    make DESTDIR="${pkgdir}" install
}
