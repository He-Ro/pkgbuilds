# Maintainer: ValHue <vhuelamo at gmail dot com>
# https://github.com/ValHue/AUR-PKGBUILDs

_python="python2"
_name="qr-tools"

pkgname="qtqr"
pkgver="1.2"
pkgrel="1"
pkgdesc="A Graphical interface QR Code generator and decoder."
url="https://launchpad.net/qr-tools"
arch=('i686' 'x86_64')
license=('GPL3')
depends=("${_python}" "${_python}-qrtools" "${_python}-pyqt4")
provides=('${pkgname}')
install="${pkgname}.install"
source=("https://launchpad.net/${_name}/trunk/${pkgver}/+download/${_name}-${pkgver}.tar.gz"
        'qtqr.desktop')
sha256sums=('7e2ec216bef3d24640acdcdeb268c2b5c6bc2bf0c8ea4181e9fb2b28ee006bc9'
            'da39d9330831012bdec33137a998301c79ed29862fbfd4279a3ec25bb2fe08a1')

build() {
    cd "${srcdir}/${_name}"
    sed -i 's/env python/env python2/' "${pkgname}.py"
}

package() {
    cd "${srcdir}/${_name}"

    install -D -m 755 'qtqr.py' "${pkgdir}/usr/bin/qtqr"
    install -D -m 644 'icon.png' "${pkgdir}/usr/share/pixmaps/qtqr.png"

    install -d "${pkgdir}/usr/share/qt4/translations"
    install -m 644 *.qm "${pkgdir}/usr/share/qt4/translations"

    install -D -m 644 "${srcdir}/qtqr.desktop" "${pkgdir}/usr/share/applications/qtqr.desktop"

    install -D -m 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
