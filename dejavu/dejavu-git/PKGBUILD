pkgname=('dejavu-git')
srcname='dejavu'
pkgdesc='Audio fingerprinting and recognition in Python '
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/worldveil/${srcname}"
license=('MIT')

depends=(
    'python2'
    'python2-pyaudio'
    'ffmpeg'
    'python2-pydub'
    'python2-numpy'
    'python2-scipy'
    'python2-matplotlib'
    'mysql-python'
)
makedepends=('git' 'python2-setuptools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${srcname}::git+https://github.com/worldveil/${srcname}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

package() {
    cd "${srcdir}/${srcname}"

    python2 setup.py install --root="${pkgdir}" --optimize=1
}
