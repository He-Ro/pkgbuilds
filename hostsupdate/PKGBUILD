pkgname=('hostsupdate-git')
srcname='hostsupdate'
pkgver='r1'
pkgrel='1'
pkgdesc='Update hosts.d lists'
arch=('any')
url='https://github.com/nfnty/hostsupdate.git'
license=('MIT')

depends=('python' 'python-requests')
provides=('hostsupdate')
conflicts=('hostsupdate')

source=(
    "${srcname}::git+https://github.com/nfnty/hostsupdate.git"
    'hostsupdate.service'
    'hostsupdate.timer'
)
sha256sums=(
    'SKIP'
    'f5372577ff97084dc411afbd199430c029c52cdb158b022939e4e037165cd333'
    '2cef073ddada7c87a561e54958243d9639cf2023e877dc0c2976383966d30c8b'
)

backup=(
    'etc/hostsupdate/excluded.json'
    'etc/hostsupdate/invalid.json'
    'etc/hostsupdate/sources.json'
)

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

package() {
    cd "${srcdir}/${srcname}"

    # systemd
    install -D --mode=644 "${srcdir}/hostsupdate.service" "${pkgdir}/usr/lib/systemd/system/hostsupdate.service"
    install -D --mode=644 "${srcdir}/hostsupdate.timer" "${pkgdir}/usr/lib/systemd/system/hostsupdate.timer"

    # script
    install -D --mode=755 hostsupdate.py "${pkgdir}/usr/bin/hostsupdate.py"

    # config
    install --directory --mode=755 "${pkgdir}/etc/hostsupdate"
    install --mode=644 --target-directory="${pkgdir}/etc/hostsupdate" "config/"{excluded.json,invalid.json,sources.json}
}
