pkgname=('python-mpd2-git')
srcname='python-mpd2'
pkgdesc='Client interface for the Music Player Daemon'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/Mic92/${srcname}"
license=('GPL3')

depends=('python')
makedepends=('git' 'python-setuptools')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${srcname}::git+https://github.com/Mic92/${srcname}.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}
package() {
    cd "${srcdir}/${srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
