pkgname=('bspwm-git')
srcname='bspwm'
pkgver='r1'
pkgrel='1'
pkgdesc='A tiling window manager based on binary space partitioning'
arch=('i686' 'x86_64')
url='https://github.com/nfnty/bspwm'
license=('custom:BSD')

depends=('libxcb' 'xcb-util' 'xcb-util-wm')
optdepends=('jansson')
makedepends=('git' 'jansson')
provides=('bspwm')
conflicts=('bspwm')

source=("${srcname}::git+https://github.com/nfnty/bspwm.git#branch=json_next")
sha512sums=('SKIP')

options=('!strip')

prepare() {
    cd "${srcdir}/${srcname}"

    git cherry-pick --no-commit 3d26201702b11d0c839b8594bbce4ce443471b40
    git cherry-pick --no-commit 56c8025e4db62eaa80d99093d3d0d0e287485da7
}

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

build() {
    cd "${srcdir}/${srcname}"

    make PREFIX='/usr' debug
}

package() {
    cd "${srcdir}/${srcname}"

    make PREFIX='/usr' DESTDIR="${pkgdir}" install
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/${srcname}/LICENSE"
}
