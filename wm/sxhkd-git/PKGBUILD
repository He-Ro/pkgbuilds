pkgname=('sxhkd-git')
srcname='sxhkd'
pkgver='r1'
pkgrel='1'
pkgdesc='Simple X hotkey daemon'
arch=('i686' 'x86_64')
url='https://github.com/baskerville/sxhkd'
license=('custom:BSD')

depends=('libxcb' 'xcb-util-keysyms')
makedepends=('git' 'xcb-util')
provides=('sxhkd')
conflicts=('sxhkd')

source=("${srcname}::git+https://github.com/baskerville/sxhkd.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

build() {
    cd "${srcdir}/${srcname}"

    make PREFIX='/usr'
}

package() {
    cd "${srcdir}/${srcname}"

    make PREFIX='/usr' DESTDIR="${pkgdir}" install
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
