pkgname=('python-xcffib-git')
srcname='xcffib'
pkgdesc='A drop-in replacement for xpyb based on cffi'
pkgver='r1'
pkgrel='1'
arch=('any')
url="https://github.com/tych0/${srcname}"
license=('Apache')

depends=('python-six' 'python-cffi' 'libxcb')
makedepends=(
    'git'
    'python-setuptools'
    'python-cffi'
    'python-six'
    'libxcb'
    'ghc'
    'haskell-xcb-types'
    'haskell-language-python'
    'haskell-filemanip'
    'haskell-split'
    'haskell-mtl'
    'haskell-attoparsec'
    'haskell-hunit'
    'cabal-install'
    'haskell-test-framework'
    'haskell-test-framework-hunit'
    'haskell-optparse-applicative'
)
checkdepends=(
    'flake8'
    'parallel'
    'python-nose'
    'xorg-server-xvfb'
    'xorg-xeyes'
)
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${srcname}::git+https://github.com/tych0/${srcname}.git")
sha512sums=('SKIP')

options=('!strip')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

build() {
    cd "${srcdir}/${srcname}"

    make
}
check() {
    cd "${srcdir}/${srcname}"

    make check
}

package() {
    cd "${srcdir}/${srcname}"

    python setup.py install --root="${pkgdir}" --optimize=1
}
