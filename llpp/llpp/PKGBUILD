# Maintainer: Daan van Rossum <d.r.vanrossum at gmx dot de>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=llpp
pkgver=20
pkgrel=7
pkgdesc='Lightweight, fast and featureful PDF viewer based on MuPDF'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('GPL')
depends=('mupdf' 'openssl' 'openjpeg2' 'libx11' 'desktop-file-utils')
makedepends=('ocaml' 'ninja' 'ocaml-lablgl')
source=(http://repo.or.cz/w/llpp.git/snapshot/53f1bca0140d03d9f2e1437a4ad0dbc3f7e85ce0.tar.gz
	build.ninja)
install=llpp.install
sha256sums=('SKIP'
            'db0e0c8b4ab1ae0cb12ab04f186d84f9713a187303922d0b14b6b194656f8125')

optdepends=(
  # llpp
  'xsel: Text selection'
  # llppac
  'file: automatic file type recognition'
  "gzip: handling of MIME types 'application/x-{gzip,compress}'"
  "xz: handling of MIME type 'application/x-xz'"
  "bzip2: handling of MIME type 'application/x-bzip2'"
  'djvulibre: djvu conversion'
  'ghostscript: ps, dvi, and djvu conversion'
  'princexml: html conversion'
  'unoconv: conversion of office documents'
  'librsvg: preferred option for svg conversion'
  'inkscape: alternative option for svg conversion'
  'imagemagick: image conversion'
  'antiword: conversion of Microsoft Word (.doc) documents (option 2)'
  'zip: png and jpeg conversion'
  'texlive-core: dvi conversion'
)
options=('!strip')

build() {
  cp build.ninja $pkgname
  cd $pkgname
  sed -i -e 's/openjpeg/openjp2 -lX11/' configure.sh
  sh configure.sh .
  ninja
}

package() {
  cd $pkgname
  install -Dm755 build/llpp "$pkgdir"/usr/bin/llpp
  install -m755 misc/llppac "$pkgdir"/usr/bin/llppac

  install -Dm0644 misc/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
}
