pkgname=('asp-git')
srcname='asp'
pkgver='r1'
pkgrel='1'
pkgdesc='Arch Build Source Management Tool'
arch=('any')
url='https://github.com/falconindy/asp'
license=('MIT')

depends=('awk' 'bash' 'git')
makedepends=('asciidoc')
provides=('asp')
conflicts=('asp')

source=("${srcname}::git+https://github.com/falconindy/asp.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

build() {
    cd "${srcdir}/${srcname}"

    make
}

package() {
    cd "${srcdir}/${srcname}"

    make PREFIX='/usr' DESTDIR="${pkgdir}" install

    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/asp/LICENSE"
}
