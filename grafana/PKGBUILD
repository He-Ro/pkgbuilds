pkgname=('grafana')
srcname='grafana'
pkgdesc='Feature rich metrics dashboard and graph editor'
pkgver='2.6.0'
pkgrel='1'
arch=('i686' 'x86_64')
url="https://github.com/grafana/${srcname}"
license=('Apache')

makedepends=('git' 'go' 'godep' 'nodejs' 'nodejs-grunt-cli' 'npm' 'phantomjs')
provides=("${pkgname[0]%-git}")
conflicts=("${pkgname[0]%-git}")

source=("${srcname}::git+https://github.com/grafana/${srcname}.git#tag=v${pkgver}")
sha512sums=('SKIP')

_url_go="${url#*//}"

prepare() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    mkdir --parents "${GOPATH}/src/${_url_go}"
    mv "${srcname}" "${GOPATH}/src/${_url_go%${srcname}}"

    cd "${GOPATH}/src/${_url_go}"
    go get -v -x -d ./...
    git checkout --quiet "v${pkgver}"
    godep restore

    npm install
}

build() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    cd "${GOPATH}/src/${_url_go}"
    go install -v -x -ldflags="\
-X main.version=${pkgver} \
-X main.commit=$( git rev-parse HEAD ) \
-X main.buildstamp=$( date --utc --iso-8601=seconds )" \
    ./...

    grunt --force release
}

package() {
    cd "${srcdir}"
    export GOPATH="${srcdir}/gopath"

    install --directory "${pkgdir}/usr/bin"
    install "${GOPATH}/bin/"* "${pkgdir}/usr/bin/"

    cd "${GOPATH}/src/${_url_go}"
    install --directory "${pkgdir}/usr/share/${srcname}"
    cp --recursive tmp/* "${pkgdir}/usr/share/${srcname}/"
    chmod --recursive 'u=rwX,g=rX,o=rX' "${pkgdir}/usr/share/${srcname}"
    install -D --mode='644' conf/sample.ini "${pkgdir}/etc/${srcname}/grafana.ini"
    install -D --mode='644' conf/ldap.toml "${pkgdir}/etc/${srcname}/ldap.toml"
    install -D --mode='644' packaging/deb/default/grafana-server "${pkgdir}/etc/conf.d/grafana-server"
    install --directory "${pkgdir}/var/lib/${srcname}"
}
