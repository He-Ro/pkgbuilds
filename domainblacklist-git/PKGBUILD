pkgname=('domainblacklist-git')
srcname='domainblacklist'
pkgver='r1'
pkgrel='1'
pkgdesc='Update domain blacklists'
arch=('any')
url='https://github.com/nfnty/domainblacklist.git'
license=('MIT')

depends=('python' 'python-requests')
provides=('domainblacklist')
conflicts=('domainblacklist')

source=(
    "${srcname}::git+https://github.com/nfnty/domainblacklist.git"
    'domainblacklist_fetch.service'
    'domainblacklist.service'
    'domainblacklist.timer'
)
sha512sums=(
    'SKIP'
    '713c039f605ee2cbada5389bb24eed94f136e2235a7dbc2c563dfed3eb9f9b6edf4246edcca396074374306ce667d2bcb0837bd74f9d2dfb5b92dc7889facf09'
    'a5eb588c44cd0da32ef1fd03e368b611b13b0befeb00ed3025383c83f026ef59491a043f3a6b3a09a85cf3d699417f657d03af50b5d50b8cbd9ec2c95e33fa8c'
    '3a5a18ac2b9bf86fd6856493a0be92586751c9d126af79651090c376d5f8edff6762404793968dc090b74bc3fac144b2638fa6eb0e615b6c5d0da7d0f3aed0aa'
)

backup=(
    'etc/domainblacklist/excluded.json'
    'etc/domainblacklist/invalid.json'
    'etc/domainblacklist/sources.json'
)

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

package() {
    cd "${srcdir}/${srcname}"

    # systemd
    install --directory --mode=755 "${pkgdir}/usr/lib/systemd/system"
    install --mode=644 --target-directory="${pkgdir}/usr/lib/systemd/system" \
        "${srcdir}/"{domainblacklist.service,domainblacklist_fetch.service,domainblacklist.timer}

    # script
    install -D --mode=755 domainblacklist.py "${pkgdir}/usr/bin/domainblacklist"

    # config
    install --directory --mode=755 "${pkgdir}/etc/domainblacklist"
    install --mode=644 --target-directory="${pkgdir}/etc/domainblacklist" "config/"{excluded.json,invalid.json,sources.json}
}
