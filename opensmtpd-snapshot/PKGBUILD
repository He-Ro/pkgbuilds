pkgname=('opensmtpd-snapshot')
srcname='opensmtpd'
pkgver='201505121836p1'
pkgrel='1'
pkgdesc='Free implementation of the server-side SMTP protocol, with some additional standard extensions.'
arch=('i686' 'x86_64')
url='https://github.com/OpenSMTPD/OpenSMTPD'
license=('ISC' 'custom:BSD')

depends=('libasr' 'libevent' 'openssl' 'db')
makedepends=('git' 'bison' 'autoconf' 'automake' 'libtool')
provides=('opensmtpd')
conflicts=('opensmtpd')

source=(
    "${srcname}::git+https://github.com/OpenSMTPD/OpenSMTPD.git#tag=opensmtpd-${pkgver}"
    'smtpd.service'
    'smtpd.socket'
    'mdoc.patch'
    'purge_task.patch'
)
sha512sums=(
    'SKIP'
    '6068bda683dd7378811b818046a660501ddd04bcdbba27126f915393dd5740b674d6e6906b96ede9e661bd02a32474b5143838e9368d7554bdd992fdff050c38'
    '806025d000ba34e86338bf127951e853849e077fe6941f7d0f43c362303ed191554f1d983d422f1acec22e89091517237a60da6e7ae6db1cb977fffe666e48c4'
    '17e7f22ce00363d4b0ce4393dd45dab0d981aef0c7a1b2ed2fc6d420f57b101a36ae5a27a1d4974c02ab37c8a3bbbe47610dbfbdf499ff7dfb699d53d9bfc5a0'
    '5f565d66c8e79ed7d5de1d3df3c2ce0161ecd0a66e261f3cbbe05a445842e910241334d44f94194d0af2887e41e336d747f516f5798959644e495e1fc37e360c'
)

install='opensmtpd.install'
backup=('etc/smtpd/smtpd.conf' 'etc/smtpd/aliases')

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/mdoc.patch"
    git apply "${srcdir}/purge_task.patch"

    sed -ri 's,/etc/mail,/etc/smtpd,g' smtpd/smtpd.conf

    ./bootstrap

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc/smtpd \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/smtpd \
        --with-maildir=/var/spool/mail \
        --with-privsep-path=/var/empty \
        --with-sock-dir=/run \
        --with-ca-file=/etc/ssl/certs/ca-certificates.crt \
        --with-privsep-user=smtpd \
        --with-queue-user=smtpq \
        --with-pam
}

build() {
    cd "${srcdir}/${srcname}"

    make
}

package() {
    cd "${srcdir}/${srcname}"

    make DESTDIR="${pkgdir}" install

    # install license and systemd unit files
    install -D --mode=644 LICENSE "${pkgdir}/usr/share/licenses/${srcname}/LICENSE"
    install -D --mode=644 "${srcdir}/smtpd.service" "${pkgdir}/usr/lib/systemd/system/smtpd.service"
    install -D --mode=644 "${srcdir}/smtpd.socket" "${pkgdir}/usr/lib/systemd/system/smtpd.socket"

    # install an empty aliases file (used by the default config)
    install -D --mode=644 /dev/null "${pkgdir}/etc/smtpd/aliases"
}
