pkgname=('emby-server')
srcname='emby-server'
pkgdesc='Emby Server is a home media server built on top of other popular open source technologies'
pkgver='3.0.5675.1'
pkgrel='1'
arch=('i686' 'x86_64')
url='https://github.com/MediaBrowser/MediaBrowser'
license=('GPL2')

depends=('mono' 'libmediainfo' 'sqlite' 'ffmpeg' 'imagemagick')
makedepends=('git' 'mono' 'imagemagick' 'sed')
provides=('emby-server')
conflicts=('emby-server')

source=("${srcname}::git+https://github.com/MediaBrowser/MediaBrowser.git#tag=${pkgver}")
sha512sums=('SKIP')

prepare() {
    cd "${srcdir}/${srcname}"
    MAGICKWAND="$( ldconfig -p | grep 'MagickWand.*.so$' | cut --delimiter=' ' --fields=4 )"
    sed --in-place "s/libMagickWand-6.Q8.so/${MAGICKWAND##*/}/" MediaBrowser.Server.Mono/ImageMagickSharp.dll.config
}

build() {
    cd "${srcdir}/${srcname}"

    xbuild /target:build \
        /property:Configuration='Release Mono' \
        /property:Platform='Any CPU' \
        /property:OutputPath="${srcdir}/build" \
        MediaBrowser.Mono.sln
}

package() {
    cd "${srcdir}/${srcname}"

    install --directory --mode=755 "${pkgdir}/opt"
    cp --recursive "${srcdir}/build" "${pkgdir}/opt/emby-server"
}
