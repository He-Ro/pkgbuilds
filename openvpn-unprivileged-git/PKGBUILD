pkgname=('openvpn-unprivileged-git')
srcname='openvpn'
pkgver='1'
pkgrel='1'
pkgdesc='A Secure tunneling daemon; Customized for running as unprivileged container'
arch=('i686' 'x86_64')
url='https://github.com/OpenVPN/openvpn'
license=('custom')

depends=('openssl' 'lzo' 'iproute2')
makedepends=('git')
provides=('openvpn')
conflicts=('openvpn')

source=(
    "$srcname::git+https://github.com/OpenVPN/openvpn.git#tag=v2.2.3"
    'tunsetiff.patch'
)
sha512sums=(
    'SKIP'
    'cd2a49b55b3f30de552dc72acbfddd08e31d3ffe3073fc817ac83878997192ba697bbc91b360cfd6e4a2f21d57e3e4a61d2583228ed361c57b99fb41c3cc5fa2'
)

pkgver() {
    cd "$srcdir/$srcname"

    printf 'r%s.%s\n' \
        "$(git rev-list HEAD | wc --lines)" \
        "$(git describe --always | sed 's/-/./g')"
}

prepare() {
    cd "$srcdir/$srcname"

    git apply "$srcdir/tunsetiff.patch"
}

build() {
    cd "$srcdir/$srcname"

    autoreconf --install --verbose --force

    CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --mandir=/usr/share/man \
        --enable-iproute2

    make
}

package() {
    cd "$srcdir/$srcname"

    make DESTDIR="$pkgdir" install
}
