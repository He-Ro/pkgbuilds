pkgname=('ranger-git')
srcname='ranger'
pkgver='1'
pkgrel='1'
pkgdesc='A simple, vim-like file manager'
arch=('any')
url='https://github.com/hut/ranger'
license=('GPL3')

depends=('python')
optdepends=(
    'file: for determining file types'
    'python-chardet: in case of encoding detection problems'
    'sudo: to use the "run as root"-feature'
    'libcaca: for ASCII-art image previews'
    'highlight: for syntax highlighting of code'
    'atool: for previews of archives'
    'w3m: for previews of images and html pages'
    'lynx: for previews of html pages'
    'elinks: for previews of html pages'
    'poppler: for pdf previews'
    'transmission-cli: for viewing bittorrent information'
    'mediainfo: for viewing information about media files'
    'perl-image-exiftool: for viewing information about media files'
)
makedepends=('git')
provides=('ranger')
conflicts=('ranger')

source=(
    "${srcname}::git+https://github.com/hut/ranger.git"
    "mime.patch"
)
sha256sums=(
    'SKIP'
    '7857b3482cfbb821354f222ec13e1a9d92046ee5ae154e8640425ef6a5565a5d'
)

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

prepare() {
    cd "${srcdir}/${srcname}"

    git apply "${srcdir}/mime.patch"
}

package() {
    cd "${srcdir}/${srcname}"

    python setup.py -q install --root="${pkgdir}" --optimize=1
}
