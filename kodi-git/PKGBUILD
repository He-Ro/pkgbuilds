pkgname=('kodi-git')
srcname='kodi'
pkgver=r30869.14.0a3.Helix.1687.g38e4046
pkgrel=1
pkgdesc='Software media player and entertainment hub for digital media'
arch=('i686' 'x86_64')
url='https://github.com/xbmc/xbmc'
license=('GPL2')

depends=(
    'python2-pillow' 'python2-pybluez' 'python2-simplejson'
    'mesa-demos' 'xorg-xdpyinfo'
    'bluez-libs' 'fribidi' 'glew' 'hicolor-icon-theme' 'libcdio'
    'libjpeg-turbo' 'libmariadbclient' 'libmicrohttpd' 'libpulse' 'libssh'
    'libva' 'libxrandr' 'libxslt' 'lzo' 'sdl2' 'smbclient' 'taglib' 'tinyxml'
    'yajl'
)
optdepends=(
    'afpfs-ng: Apple shares support'
    'bluez: Blutooth support'
    'libnfs: NFS shares support'
    'libplist: AirPlay support'
    'libcec: Pulse-Eight USB-CEC adapter support'
    'lirc: Remote controller support'
    'pulseaudio: PulseAudio support'
    'shairplay: AirPlay support'
    'udisks: Automount external drives'
    'unrar: Archives support'
    'unzip: Archives support'
    'upower: Display battery level'
)
makedepends=(
    'afpfs-ng' 'bluez-libs' 'boost' 'cmake' 'curl' 'cwiid' 'doxygen' 'git' 'glew'
    'gperf' 'hicolor-icon-theme' 'jasper' 'java-runtime' 'libaacs' 'libass'
    'libbluray' 'libcdio' 'libcec' 'libgl' 'libmariadbclient' 'libmicrohttpd'
    'libmodplug' 'libmpeg2' 'libnfs' 'libplist' 'libpulse' 'libssh' 'libva'
    'libvdpau' 'libxrandr' 'libxslt' 'lzo' 'nasm' 'nss-mdns' 'python2-pillow'
    'python2-pybluez' 'python2-simplejson' 'rtmpdump' 'sdl2' 'sdl_image'
    'shairplay' 'smbclient' 'swig' 'taglib' 'tinyxml' 'unzip' 'upower' 'yajl' 'zip'
)
provides=('kodi')
conflicts=('kodi')
replaces=('xbmc')

source=("$srcname::git+https://github.com/xbmc/xbmc.git#branch=Helix"
        'vaapi.patch')
sha256sums=('SKIP' 'SKIP')

install='kodi.install'

pkgver() {
    cd "$srcdir/$srcname"
    printf 'r%s.%s\n' \
        "$(git rev-list HEAD | wc --lines)" \
        "$(git describe --always | sed 's/-/./g')"
}

prepare() {
    cd "$srcdir/$srcname"

    git apply "$srcdir/vaapi.patch"

    find -type f -name *.py -exec sed 's|^#!.*python$|#!/usr/bin/python2|' -i "{}" +
    sed 's|^#!.*python$|#!/usr/bin/python2|' -i tools/depends/native/rpl-native/rpl
    sed 's/python/python2/' -i tools/Linux/kodi.sh.in
}

build() {
    cd "$srcdir/$srcname"

    TMPDIR="$srcdir/tmp"
    mkdir --parents "$TMPDIR"

    # Bootstrapping
    MAKEFLAGS=-j1 ./bootstrap

    #./configure --help
    #return 1

    # Configuring XBMC
    export PYTHON_VERSION=2  # external python v2
    ./configure --prefix='/usr' --exec-prefix='/usr' \
        --disable-silent-rules \
        --disable-shared-lib \
        --disable-debug \
        --enable-optimizations \
        --enable-gl \
        --disable-gles \
        --enable-sdl \
        --disable-vdpau \
        --enable-vaapi \
        --disable-crystalhd \
        --disable-vtbdecoder \
        --disable-openmax \
        --disable-tegra \
        --disable-profiling \
        --disable-xvba \
        --disable-joystick \
        --enable-xrandr \
        --disable-waveform \
        --disable-spectrum \
        --disable-goom \
        --disable-rsxs \
        --disable-fishbmc \
        --disable-projectm \
        --enable-x11 \
        --disable-ccache \
        --enable-alsa \
        --disable-pulse \
        --disable-ssh \
        --enable-rtmp \
        --disable-samba \
        --disable-nfs \
        --disable-afpclient \
        --disable-airplay \
        --disable-airtunes \
        --disable-ffmpeg-libvorbis \
        --disable-dvdcss \
        --disable-mid \
        --disable-hal \
        --disable-avahi \
        --disable-mdnsembedded \
        --disable-non-free \
        --disable-asap-codec \
        --enable-webserver \
        --disable-optical-drive \
        --enable-libbluray \
        --enable-texturepacker \
        --enable-external-libraries \
        --enable-udev \
        --disable-libusb \
        --disable-libcec \
        --disable-libmp3lame \
        --disable-libvorbisenc \
        --disable-libcap \
        --disable-player \
        --disable-gtest \
        --disable-codec \
        --disable-libav-compat \
        --enable-dependency-tracking \
        --with-lirc-device=/run/lirc/lircd

    # Now (finally) build
    make -j4
}

package() {
    cd "$srcdir/$srcname"
    # Running make install
    make DESTDIR="$pkgdir" install

    # Licenses
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    for licensef in LICENSE.GPL copying.txt; do
        mv "${pkgdir}/usr/share/doc/kodi/${licensef}" \
            "${pkgdir}/usr/share/licenses/${pkgname}"
    done
}
