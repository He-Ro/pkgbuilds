pkgname=('xfce4-panel-git')
srcname='xfce4-panel'
pkgver=r4267.4.11.1.61.g3cf77c8
pkgrel=1
pkgdesc='Panel for the Xfce desktop environment'
arch=('i686' 'x86_64')
url='http://git.xfce.org/xfce/xfce4-panel'
license=('GPL2')

groups=('xfce4-git')
depends=('libxfce4ui-git' 'exo-git' 'garcon-git' 'libwnck' 'startup-notification')
makedepends=('git' 'xfce4-dev-tools-git' 'gtk-doc')
provides=('xfce4-panel')
conflicts=('xfce4-panel')

source=("$srcname::git+git://git.xfce.org/xfce/xfce4-panel")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$srcname"
	printf 'r%s.%s\n' \
		"$(git rev-list HEAD | wc -l)" \
		"$(git describe --always | sed 's/-/./g' | sed 's/xfce4.panel.//g')"
}

build() {
	cd "$srcdir/$srcname"
	./autogen.sh --prefix=/usr
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-gtk-doc \
		--disable-debug \
		--enable-maintainer-mode
	make
}

package() {
	cd "$srcdir/$srcname"
	make DESTDIR="$pkgdir" install
}
