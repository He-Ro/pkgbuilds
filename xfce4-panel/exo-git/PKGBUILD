pkgname=('exo-git')
srcname='exo'
pkgver=r1536.0.10.2.140.gfef273d
pkgrel=1
pkgdesc='Support library for Xfce'
arch=('i686' 'x86_64')
url='http://git.xfce.org/xfce/exo'
license=('GPL2')

groups=('xfce4-git')
depends=('libxfce4util-git' 'gtk2' 'perl-uri' 'libxfce4ui-git')
makedepends=('git' 'xfce4-dev-tools-git' 'gtk-doc')
provides=('exo')
conflicts=('exo')

source=("$srcname::git+git://git.xfce.org/xfce/exo")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$srcname"
	printf 'r%s.%s\n' \
		"$(git rev-list HEAD | wc -l)" \
		"$(git describe --always | sed 's/-/./g' | sed 's/exo.//g')"
}

build() {
	cd "$srcdir/$srcname"
	./autogen.sh --prefix=/usr
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-gtk-doc \
		--disable-debug \
		--enable-maintainer-mode
	make
}

package() {
	cd "$srcdir/$srcname"
	make DESTDIR="$pkgdir" install
}
