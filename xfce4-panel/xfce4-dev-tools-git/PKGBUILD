pkgname=('xfce4-dev-tools-git')
srcname='xfce4-dev-tools'
pkgver=r157.4.11.0.3.ge01beca
pkgrel=1
pkgdesc='Xfce dev tools'
arch=('i686' 'x86_64')
url='http://git.xfce.org/xfce/xfce4-dev-tools'
license=('GPL2')

groups=('xfce4-git')
depends=('intltool')
makedepends=('git')
provides=('xfce4-dev-tools')
conflicts=('xfce4-dev-tools')

source=("$srcname::git+git://git.xfce.org/xfce/xfce4-dev-tools")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$srcname"
	printf 'r%s.%s\n' \
		"$(git rev-list HEAD | wc -l)" \
		"$(git describe --always | sed 's/-/./g' | sed 's/xfce4.dev.tools.//g')"
}

build() {
	cd "$srcdir/$srcname"
	./autogen.sh --prefix=/usr
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-gtk-doc \
		--disable-debug \
		--enable-maintainer-mode
	make
}

package() {
	cd "$srcdir/$srcname"
	make DESTDIR="$pkgdir" install
}
