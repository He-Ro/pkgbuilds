pkgname=('libxfce4util-git')
srcname='libxfce4util'
pkgver=r634.4.11.0.13.g8a996e2
pkgrel=1
pkgdesc='Basic utility non-GUI functions for Xfce'
arch=('i686' 'x86_64')
url='http://git.xfce.org/xfce/libxfce4util'
license=('GPL2')

groups=('xfce4-git')
depends=('glib2')
makedepends=('git' 'xfce4-dev-tools-git' 'gtk-doc')
provides=('libxfce4util')
conflicts=('libxfce4util')

source=("$srcname::git+git://git.xfce.org/xfce/libxfce4util")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$srcname"
	printf 'r%s.%s\n' \
		"$(git rev-list HEAD | wc -l)" \
		"$(git describe --always | sed 's/-/./g' | sed 's/libxfce4util.//g')"
}

build() {
	cd "$srcdir/$srcname"
	./autogen.sh --prefix=/usr
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-gtk-doc \
		--disable-debug \
		--enable-maintainer-mode
	make
}

package() {
	cd "$srcdir/$srcname"
	make DESTDIR="$pkgdir" install
}
