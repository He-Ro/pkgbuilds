pkgname=('garcon-git')
srcname='garcon'
pkgver=r676.0.3.0.17.ga1d5fa4
pkgrel=1
pkgdesc='Freedesktop.org compliant menu library'
arch=('i686' 'x86_64')
url='http://git.xfce.org/xfce/garcon'
license=('GPL2')

groups=('xfce4-git')
depends=('glib2' 'libxfce4util-git')
makedepends=('git' 'xfce4-dev-tools-git' 'gtk-doc')
provides=('garcon')
conflicts=('garcon')

source=("$srcname::git+git://git.xfce.org/xfce/garcon")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$srcname"
	printf 'r%s.%s\n' \
		"$(git rev-list HEAD | wc -l)" \
		"$(git describe --always | sed 's/-/./g' | sed 's/garcon.//g')"
}

build() {
	cd "$srcdir/$srcname"
	./autogen.sh --prefix=/usr
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-gtk-doc \
		--disable-debug \
		--enable-maintainer-mode
	make
}

package() {
	cd "$srcdir/$srcname"
	make DESTDIR="$pkgdir" install
}
