# vim:set ts=2 sw=2 et:
# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Zeqadious <zeqadious.at.gmail.dot.com>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>

_prefix=/usr

pkgname=xbmc
pkgver=13.2
_codename=Gotham
pkgrel=6
pkgdesc="A software media player and entertainment hub for digital media"
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL2')
depends=(
'hicolor-icon-theme' 'lame' 'libass' 'libbluray' 'libcdio' 'libmad'
'libmicrohttpd' 'libmodplug' 'libmpeg2' 'libmysqlclient'
'libsamplerate' 'libva' 'libxrandr' 'libxslt'
'lzo' 'mesa' 'mesa-demos' 'python2' 'python2-pillow' 'rtmpdump' 'sdl_image'
'taglib' 'tinyxml' 'unzip' 'xorg-xdpyinfo' 'yajl'
)
makedepends=(
'afpfs-ng' 'libplist' 'boost' 'cmake' 'doxygen'
'gperf' 'jasper' 'java-runtime-headless' 'nasm' 'swig' 'taglib' 'udisks'
'zip'
)
optdepends=(
'polkit: power management'
'unrar: access compressed files without unpacking them'
'upower: display battery level'
)
install="${pkgname}.install"
source=(
  "xbmc-$pkgver-$_codename.tar.gz::https://github.com/xbmc/xbmc/archive/$pkgver-$_codename.tar.gz"
)
sha256sums=(
  'acbf5dffa3034a3406240ee3cf81e721386cd383d23d056ddde1f769e050e585'
)

groups=('modified')

prepare() {
  cd "$srcdir/xbmc-$pkgver-$_codename"

  find -type f -name *.py -exec sed 's|^#!.*python$|#!/usr/bin/python2|' -i "{}" +
  sed 's|^#!.*python$|#!/usr/bin/python2|' -i tools/depends/native/rpl-native/rpl
  sed 's/python/python2/' -i tools/Linux/xbmc.sh.in
}

build() {
  cd "$srcdir/xbmc-$pkgver-$_codename"

  # Bootstrapping XBMC
  ./bootstrap

  # Configuring XBMC
  export PYTHON_VERSION=2  # external python v2
  ./configure --prefix=$_prefix --exec-prefix=$_prefix \
    --disable-silent-rules \
    --disable-shared-lib \
    --disable-debug \
    --enable-optimizations \
    --enable-gl \
    --disable-gles \
    --enable-sdl \
    --disable-vdpau \
    --enable-vaapi \
    --disable-crystalhd \
    --disable-vtbdecoder \
    --disable-openmax \
    --disable-tegra \
    --disable-profiling \
    --disable-xvba \
    --disable-joystick \
    --enable-xrandr \
    --disable-waveform \
    --disable-spectrum \
    --disable-goom \
    --disable-rsxs \
    --disable-fishbmc \
    --disable-projectm \
    --enable-x11 \
    --disable-ccache \
    --enable-alsa \
    --disable-pulse \
    --disable-ssh \
    --enable-rtmp \
    --disable-samba \
    --disable-nfs \
    --disable-afpclient \
    --disable-airplay \
    --disable-airtunes \
    --disable-ffmpeg-libvorbis \
    --disable-dvdcss \
    --disable-mid \
    --disable-hal \
    --disable-avahi \
    --disable-mdnsembedded \
    --disable-non-free \
    --disable-asap-codec \
    --enable-webserver \
    --disable-optical-drive \
    --enable-libbluray \
    --enable-texturepacker \
    --enable-udev \
    --disable-libusb \
    --disable-libcec \
    --disable-libmp3lame \
    --disable-libvorbisenc \
    --disable-libcap \
    --disable-player \
    --disable-gtest \
    --disable-codec \
    --disable-external-libraries \
    --disable-libav-compat \
    --enable-dependency-tracking \
    --with-lirc-device=/run/lirc/lircd

  # Now (finally) build
  make -j4
}

package() {
  cd "$srcdir/xbmc-$pkgver-$_codename"
  # Running make install
  make DESTDIR="$pkgdir" install

  # Tools
  install -D -m 0755 "$srcdir/xbmc-$pkgver-$_codename/tools/TexturePacker/TexturePacker" \
    "${pkgdir}$_prefix/lib/xbmc/"

  # Licenses
  install -dm755 "${pkgdir}$_prefix/share/licenses/$pkgname"
  for licensef in LICENSE.GPL copying.txt; do
    mv "${pkgdir}$_prefix/share/doc/xbmc/$licensef" \
      "${pkgdir}$_prefix/share/licenses/$pkgname"
  done
}
