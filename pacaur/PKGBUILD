# Maintainer: Spyhawk

pkgname=pacaur
pkgver=4.2.14
pkgrel=1
pkgdesc="A fast workflow AUR helper using cower as backend"
arch=('any')
url="https://github.com/rmarquis/pacaur"
license=('GPL')
depends=('cower' 'expac' 'sudo' 'pacman')
makedepends=('perl')
backup=('etc/xdg/pacaur/config')
source=("https://github.com/rmarquis/$pkgname/archive/$pkgver.tar.gz")
md5sums=('c0d00b121d987d39952ddf56bcd6357a')

build() {
    cd "$pkgname-$pkgver"
    pod2man --utf8 --section=8 --center="Pacaur Manual" --name="PACAUR"\
        --release="$pkgname $pkgver" ./README.pod > ./pacaur.8
}

package() {
    cd "$pkgname-$pkgver"

    mkdir -p $pkgdir/etc/xdg/pacaur
    install -D -m644 ./config $pkgdir/etc/xdg/pacaur/config
    mkdir -p $pkgdir/usr/bin
    install -D -m755 ./$pkgname $pkgdir/usr/bin/$pkgname
    mkdir -p $pkgdir/usr/share/zsh/site-functions
    install -D -m644 ./zsh.completion\
        $pkgdir/usr/share/zsh/site-functions/_pacaur
    mkdir -p $pkgdir/usr/share/man/man8
    install -D -m644 ./pacaur.8 $pkgdir/usr/share/man/man8/pacaur.8
}

