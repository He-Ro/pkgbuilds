pkgname=('cower-git')
srcname='cower'
pkgver='r1'
pkgrel='1'
pkgdesc='A simple AUR agent with a pretentious name'
arch=('i686' 'x86_64')
url='https://github.com/falconindy/cower'
license=('MIT')

depends=('curl' 'openssl' 'pacman' 'yajl')
makedepends=('git' 'perl')
provides=('cower')
conflicts=('cower')

source=("${srcname}::git+https://github.com/falconindy/cower.git")
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${srcname}"

    printf 'r%s.%s\n' \
        "$( git rev-list HEAD | wc --lines )" \
        "$( git describe --always | sed 's/-/./g' )"
}

prepare() {
    cd "${srcdir}/${srcname}"

    sed '/^$/q' cower.c >LICENSE
}

build() {
    cd "${srcdir}/${srcname}"

    make
}

package() {
    cd "${srcdir}/${srcname}"

    make PREFIX='/usr' DESTDIR="${pkgdir}" install
    install -D --mode=644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
